package(default_visibility = ["//visibility:public"])

load("@angular//:index.bzl", "ng_module")
load("@build_bazel_rules_typescript//:defs.bzl", "ts_config", "ts_devserver")
load("@build_bazel_rules_nodejs//:defs.bzl", "rollup_bundle", "web_package", "history_server", "inject_html")

ts_config(
    name = "tsconfig-test",
    src = "tsconfig-test.json",
    deps = [":tsconfig.json"],
)

ng_module(
    name = "src",
    srcs = glob(["*.ts"]),
    assets = [
        ":app.component.html",
    ],
    tsconfig = ":tsconfig.json",
    deps = [
        "//src/hello-world",
        "//src/material",
        "//src/todos",
        "@angular//packages/core",
        "@angular//packages/router",
        "@npm//@ngrx/store",
    ],
)

inject_html(
    name = "index",
    index_html = "index.html",
    assets = [
        "@npm//node_modules/@angular/material:prebuilt-themes/deeppurple-amber.css",
    ],
    inject = [
        "bundle.js",
    ],
)


# This devserver is written in Go and is super-fast.
# It doesn't run any bundler or code splitter. Instead, it concatenates
# UMD JavaScript code on-the-fly in-memory.
# This scales really well for massive codebases.
ts_devserver(
    name = "devserver",
    # serve these files rooted at /
    additional_root_paths = [
        "npm/node_modules/@angular/material/prebuilt-themes",
        "npm/node_modules/@ngrx/store/bundles",
    ],
    # Start from the development version of the main
    entry_module = "angular_bazel_example/src/main.dev",
    bootstrap = [
        "@npm//node_modules/zone.js:dist/zone.min.js",
    ],
    scripts = [
        ":require.config.js",
        ":module-id.js",
        "@npm//node_modules/tslib:tslib.js",
    ],
    # This is the URL we'll point our <script> tag at
    serving_path = "/bundle.js",
    # Serve these files in addition to the JavaScript bundle
    static_files = [
        "@npm//node_modules/@angular/material:prebuilt-themes/deeppurple-amber.css",
        "@npm//node_modules/@ngrx/store:bundles/store.umd.min.js",
        ":index"
    ],
    # Tell Bazel to build the sources first
    deps = ["//src"],
)

rollup_bundle(
    name = "bundle",
    # These Angular routes may be lazy-loaded at runtime.
    # So we tell Rollup that it can put them in separate JS chunks
    # (code-splitting) for faster application startup.
    # In the future, we could automatically gather these from statically
    # analyzing the Angular sources.
    additional_entry_points = [
        "src/hello-world/hello-world.module.ngfactory",
        "src/todos/todos.module.ngfactory",
    ],
    entry_point = "src/main",
    deps = [
        "//src",
        "@npm//@ngrx/store",
    ],
)

web_package(
    name = "prodapp",
    index_html = "index.html",
    # do not sort
    assets = [
        "@npm//node_modules/@angular/material:prebuilt-themes/deeppurple-amber.css",
        "@npm//node_modules/zone.js:dist/zone.min.js",
        "@npm//node_modules/systemjs:dist/system.js",
        ":bundle.min.js",
    ],
    data = [
        ":bundle",
    ],
)

history_server(
    name = "prodserver",
    data = [":prodapp"],
    templated_args = ["src/prodapp"],
)
